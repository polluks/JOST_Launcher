;****************************************
; 1.3 JOST Launcher
; 2024
; Amiga version
; Author: Youen CHENE
;
; Game/Demo launcher for JOST exe to run .slave for WHDLOAD compatible with 1.3 kickstart
;
;****************************************

;NoCli
WBStartup
;CloseWorkBench_

DEFTYPE.w

DELIMITER$=";"

#InitBufferCursor=2

currentScanDir$="RAM:"

AMIGA

NEWTYPE.FileInfoBlock
   _DiskKey.l
   _DirEntryType.l
   _FileName.b[108]
   _Protection.l
   _EntryType.l
   _Size.l
   _NumBlocks.l
   ds_Days.l
   ds_Minute.l
   ds_Tick.l
   _Comment.b[80]
   _Reserved.b[36]
End NEWTYPE

DEFTYPE .FileInfoBlock myfib        ;myfib holds infoblock

NEWTYPE.Slave
    name$
    path$
End NEWTYPE

Dim List slaves2.Slave(100)


Statement listdir{dir$,curdir$}
  SHARED myfib, slaves2, DELIMITER$
  USEPATH myfib
  lock.l=Lock_(&dir$,-2)                   ;lock is dos system to access files
  countGame.b=0
  If Examine_(lock,myfib)<>0               ;if we get lock on p$
    While ExNext_(lock,myfib)<>0           ;skip through files
      If \_DirEntryType<0                  ;if its not a directory
        filename$ = LSet$(Peek$(&\_FileName),30)  ;30 56 or 106 depends of FFS         
        If Len(filename$)>5
            If Instr(filename$,".Slav")>0  OR  Instr(filename$,".slav")>0
                name$=curdir$
                If countGame > 0
                    name$=name$ +" ("+  StripTrail$(filename$,Asc(" ")) +")"
                EndIf
                NPrint name$,DELIMITER$,dir$,"/",filename$
                countGame+1
            EndIf
        EndIf
      Else
        fi$=Peek$(&\_FileName)
        listdir{dir$+"/"+fi$,fi$}
      EndIf
    Wend
  EndIf
  UnLock_ lock
End Statement



.MainLoop:
 currentScanDir$="Games:Games"

 Dim List slaves.Slave(100)

 Gosub LoadConfig
 VWAIT 50

cursorY.b=0
previousCursorY.b=0
cursorPage.b=0

 BLITZ  
 BitMap 0,640,256,2
 BitMapOutput 0 
 Slice 0,44,640,256,$fff9,2,0,4,640,640
 Use Slice 0
 PalRGB 0,0,0,0,0
 PalRGB 0,1,15,15,15
 Use Palette 0
 CLS 0
 Colour 1,0
 Locate 1,0.5+cursorY
 previousCursorY=cursorY

 Print ">"
 Locate 1,30.7
 Print "HELP : R or 0 to scan folders"
 Line 10,240,630,240,1 
 Gosub DisplayList

 Show 0
 updateCursor.b=0
 bufferCursor=#InitBufferCursor
 launch.q=0
 pathToLaunch$=""

 Repeat
    previousCursorY=cursorY
    bufferCursor=bufferCursor-1
    ; Scan dir
    If RawStatus($13)
        QAMIGA
        NPrint "Scan dir : ",currentScanDir$
        Gosub ScanDir
        Gosub LoadConfig
        VWAIT 50
        BLITZ
        Gosub DisplayList
    End If
    ; Enter
    If RawStatus($44)
      Locate 30,30.7
      NPrint "Launching : ",slaves(cursorPage+cursorY)\name
      pathToLaunch$=slaves(cursorPage+cursorY)\path
      launch=-1
    End If
    ; Up
    If ((RawStatus($4C) OR Joyy(1)=-1) AND cursorY>0 AND bufferCursor<=0) 
      cursorY=cursorY-1
      updateCursor=-1
    EndIf
    ; down
    If ((RawStatus($4F) OR Joyy(1)=1 ) AND cursorY<10 AND bufferCursor<=0)
      cursorY=cursorY+1
      updateCursor=-1
    EndIf
    ; 4f left 4e right
    ; updateCursor
    If (updateCursor)
       Colour 0,0
       Locate 1,0.5+previousCursorY
       Print ">"
       Colour 1,0
       Locate 1,0.5+cursorY
       Print ">"
       bufferCursor=#InitBufferCursor
       updateCursor=0
    EndIf
    VWAIT
 Until (RawStatus($45) OR launch)
 VWait 100
 Free BitMap 0
 FreeSlices

If (launch)
  AMIGA 
  WBenchToFront_
  Execute_ command$,0,0
Repeat
 vwait
 Until (RawStatus($45))
End If

End


.LoadConfig:
 USEPATH slaves() 
 ResetList slaves()
 While NextItem(slaves())
    KillItem slaves()
 Wend
ResetList slaves()
 If ReadFile (0,"slaves.data")
    FileInput 0
    While NOT Eof(O)
        If AddItem(slaves())
            l$=Edit$(128)
            delimiterPos=Instr(l$,DELIMITER$)
            slaves()\name=Left$(l$,delimiterPos-1)
            slaves()\path=UnRight$(l$,delimiterPos)
        EndIf
    Wend
    CloseFile 0
    DefaultInput
 EndIf
 RETURN

.ScanDir:
 If WriteFile(0,"slaves.data") ;
  FileOutput 0
  listdir {currentScanDir$,""}
  CloseFile 0
  DefaultOutput
 EndIf
 RETURN

.DisplayList:
 DefaultOutput
 USEPATH slaves() 
 ResetList slaves()
 BitMapOutput 0 
 Colour 1,0
 Locate 1,0.5
 i=0
 While NextItem(slaves())
  If (i<20)
    Locate 3,0.5+i
    Print \name
    i=i+1
  EndIf
 Wend
 RETURN

